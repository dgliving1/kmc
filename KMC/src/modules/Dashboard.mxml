<?xml version="1.0" encoding="utf-8"?>
<!---
	 KMC Dashboard module
	 First phase - Launch link and show basic graphs
-->
<modules:KmcModule xmlns:mx="http://www.adobe.com/2006/mxml" 
				   xmlns:modules="com.kaltura.kmc.modules.*"
				   preloader="com.kaltura.preloaders.KmcPreloader" 
				   backgroundColor="#034F57" layout="vertical"
				   verticalAlign="top" styleName="mainView"
				   minHeight="520" minWidth="950" 
				   verticalGap="0"
				   xmlns:panels="com.kaltura.kmc.modules.dashboard.panels.*" 
				   >


	<mx:Metadata>
			[ResourceBundle("kdashboard")]
		</mx:Metadata>

	<mx:Script>
		<![CDATA[
			import com.kaltura.analytics.GoogleAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTracker;
			import com.kaltura.kmc.modules.dashboard.DashboardManager;
			
			import mx.controls.Alert;
			import mx.resources.IResourceManager;
			import mx.resources.ResourceManager;
			
			/**
			 * KMC is responsible to use the value of  
			 * this const as the id of this module.
			 * */
			public static const NAME:String = "dashboard";
			
			// keep this public, it is used in the panels
			public static const BUTTON_WIDTH_SIZE:int = 140;
			
			private static const VERSION:String = "2.0";

			[Bindable]
			/**
			 * readable user name
			 * */
			private var _userName:String = '';
			

			/**
			 * user name for google analytics
			 * */
			private var _gaUserId:String;

			/**
			 * google analytics identifier
			 * */
			private var _gaUrchinNumber:String;

			
			override public function getModuleName():String {
				return NAME;
			}
			override public function getModuleVersion():String {
				return VERSION;
			}
			
			override public function showSubtab(subtab:String, data:Object = null):void {
				// dashboard don't have tabs, empty implementation
			}

			/**
			 * Selecting the right state for the user - first time or not.
			 */
			override protected function start():void {
				trace("=========================");
				trace(" KMC Dashboard " + VERSION);
				trace("=========================");
				Security.allowDomain('*');

				_gaUrchinNumber = _flashvars.urchinnumber;

				_userName = _userInfo.user.fullName;
				_gaUserId = _flashvars.uid;

				if (_flashvars.firstlogin == 'true') {
					currentState = 'firstTimeUserState';
				}

				DashboardManager.instance.kc = _kc;
				var confFile:XML = new XML(_uiconf.confFile);
				var links:XMLList = confFile.links.link;

				uploadPanel.quickStartURL = links.(@id == "upload")[0].@path; 		//_flashvars.uploaddoclink;
				embedPanel.quickStartURL = links.(@id == "embed")[0].@path; 		//_flashvars.embeddoclink;
				customizePanel.quickStartURL = links.(@id == "customize")[0].@path; //_flashvars.customizedoclink;

				DashboardManager.instance.app = this;
				DashboardManager.instance.getData();
				DashboardManager.instance.openKcwFunction = _flashvars.openCw; 

				/// set Alert locale for all this application
				var rm:IResourceManager = ResourceManager.getInstance(); 
				Alert.yesLabel = rm.getString('kdashboard', 'yes');
				Alert.noLabel = rm.getString('kdashboard', 'no');
				Alert.okLabel = rm.getString('kdashboard', 'ok');
				Alert.cancelLabel = rm.getString('kdashboard', 'cancel');

				var ka:KAnalyticsTracker = KAnalyticsTracker.getInstance();
				ka.init(DashboardManager.instance.kc, "Dashboard", VERSION, _gaUserId);
				
				if (stage) {
					initGoogleTracker(null);
				}
				else {
					addEventListener(Event.ADDED_TO_STAGE, initGoogleTracker);
				}
				
				permissionManager.applyAllAttributes(this,NAME);
			}

			
			/**
			 * Initialize Google analytics tracker.
			 * This can ionly be done after there is a stage, because in debug  
			 * mode (flashvars: gaDebug=true) the tracker requires the stage.
			 * */
			private function initGoogleTracker(event:Event):void {
				var ga:GoogleAnalyticsTracker = GoogleAnalyticsTracker.getInstance();
				ga.init(DashboardManager.instance.kc.partnerId, _gaUserId, this, "KMC/Dashboard",
						_gaUrchinNumber, "AS3", _flashvars.gadebug == "true" ? true : false);
			}
		]]>
	</mx:Script>

	<mx:HBox width="100%" styleName="tabsContainer" height="39" verticalScrollPolicy="off">
		<mx:Label text="{_userName + ','}" id="userNameLabel" styleName="nameLabelStyle"/>
		<mx:Label text="{resourceManager.getString('kdashboard','main_screen_welcome_title',[_userInfo.role.name, DashboardManager.instance.publisherName])}" 
				  id="welcomeLable" styleName="titleWelcomeLabelStyle" />
	</mx:HBox>
	<mx:HBox width="100%" height="100%" styleName="innerView">
		
		<mx:VBox width="65%" height="100%">
					<panels:UploadPanel id="uploadPanel" />
					<panels:EmbedPanel id="embedPanel"/>
					<panels:CustomizePanel id="customizePanel"/>
		</mx:VBox>
		<mx:VBox width="35%" height="100%">
					<panels:ChartsPanel id="chartsPanel" width="100%" height="65%" />
					<panels:AccountPanel id="accountPanel" height="35%"/>
			
		</mx:VBox>
		

	</mx:HBox>
</modules:KmcModule>
