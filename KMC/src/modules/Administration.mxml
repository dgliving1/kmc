<?xml version="1.0" encoding="utf-8"?>
<modules:KmcModule xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:modules="com.kaltura.kmc.modules.*" 
				   xmlns:control="com.kaltura.kmc.modules.admin.control.*"
				   xmlns:view="com.kaltura.kmc.modules.admin.view.*" 
				   xmlns:model="com.kaltura.kmc.modules.admin.model.*" styleName="adminModuleMain" 
				   layout="vertical" minHeight="520" minWidth="950" 
				   creationComplete="creationCompleteHandler(event)" enabled="{!_model.isLoading}">
	<mx:Metadata>
		[ResourceBundle("admin")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.kaltura.kmc.business.JSGate;
			import com.kaltura.kmc.modules.admin.control.events.GeneralEvent;
			import com.kaltura.kmc.modules.admin.control.events.ListItemsEvent;
			import com.kaltura.kmc.modules.admin.control.events.PartnerEvent;
			import com.kaltura.kmc.modules.admin.model.AdminModelLocator;
			import com.kaltura.kmc.modules.admin.view.IAdminSubtab;
			
			import mx.events.FlexEvent;
			private static const VERSION:String = "0.1";

			/**
			 * KMC is responsible to use the value of
			 * this const as the id of this module.
			 * */
			public static const NAME:String = "admin";
			
			
			[Bindable]
			/**
			 * module model
			 * */
			private var _model:AdminModelLocator = AdminModelLocator.getInstance();
			
			
			/**
			 * in case we want to start from a tab other than the
			 * first, name of the initial tab
			 * */
			private var _initialTab:String;
			
			
			/**
			 * indicates start() has finished executing so all initial
			 * data is present, as well as cretionComplete.
			 * */
			protected var _ready:Boolean = false;


			override public function getModuleName():String {
				return NAME;
			}


			override public function getModuleVersion():String {
				return VERSION;
			}


			override public function showSubtab(subtab:String, data:Object = null):void {
				// show the tab
				_initialTab = subtab;
				if (_ready) {
					switchToTab(subtab);
				}
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void {
				if (_ready && _initialTab) {
					switchToTab(_initialTab);
				}
				else {
					_ready = true;
				}
			}


			override protected function start():void {
				Security.allowDomain("*");
				
				trace("=========================");
				trace(" KMC Administration " + VERSION);
				trace("=========================");
				
				
				initModel();
				if (_ready && _initialTab) {
					switchToTab(_initialTab);
				}
				else {
					_ready = true;
				}
			}
			
			
			private function switchToTab(tab:String = ""):void {
				switch (tab) {
					case 'roles':
						secTln.selectedIndex = 1;
						roles.refreshData();
						break;
					case 'users':
					default:
						secTln.selectedIndex = 0;
						users.refreshData();
						break;
				}
			}
			
			
			/**
			 * initialize administration model
			 * */
			private function initModel():void {
				_model.kc = _kc;
				var uiConf:XML = XML(_uiconf.confFile)
				_model.usersModel.usersUpgradeLink =  uiConf.upgradeUsersPage.text().toString();
				_model.rolesModel.partnerPermissionsUiconf = permissionManager.partnerUIDefinitions;
				// list partner permissions
				var cge:CairngormEvent = new ListItemsEvent(ListItemsEvent.LIST_PARTNER_PERMISSIONS);
				cge.dispatch();
				cge = new PartnerEvent(PartnerEvent.GET_PARTNER_DATA);
				cge.dispatch();
			}


			/**
			 * change url
			 * */
			private function onItemClick(event:Event):void {
				(contentView.selectedChild as IAdminSubtab).refreshData();
				var label:String = contentView.selectedChild.id;
				JSGate.writeUrlHash( Administration.NAME, label);
			}


			/**
			 * event handler that allows the inner panels to enable or disable HTML headers
			 * */
			protected function enableHeaderHandler(event:GeneralEvent):void
			{
				var bEnable:Boolean = event.data;
				enableHtmlTabs(bEnable);
			}

		]]>
	</mx:Script>
	<!-- the FrontController, containing Commands specific to this appliation -->
	<control:AdminController/>
	

	<mx:HBox width="100%" styleName="tabsContainer" enabled="{true}">
		<mx:TabBar id="secTln" styleName="tln" itemClick="onItemClick(event)" buttonMode="true"
				   dataProvider="contentView"/>
	</mx:HBox>
	<mx:ViewStack id="contentView" width="100%" height="100%" styleName="adminViewstack"
				  horizontalScrollPolicy="off" verticalScrollPolicy="off">
		<view:Users id="users" label="{resourceManager.getString('admin','users_tab')}" enableHeader="enableHeaderHandler(event)"
					width="100%" model="{_model.usersModel}" rolesModel="{_model.rolesModel}"/>
		<view:Roles id="roles" label="{resourceManager.getString('admin','roles_tab')}" enableHeader="enableHeaderHandler(event)"
					width="100%" model="{_model.rolesModel}" />
	</mx:ViewStack>

</modules:KmcModule>
