<?xml version="1.0" encoding="utf-8"?>
<mx:DataGrid xmlns:mx="http://www.adobe.com/2006/mxml" rowHeight="36"
			 allowMultipleSelection="true" headerRelease="headerReleaseHandler(event)" 
			 creationComplete="datagrid1_creationCompleteHandler(event)"
			 itemClick="setSelection(event)" change="setSelection(event)">
	<mx:Metadata>
		[Event(name="orderByChanged", type="flash.events.Event")] 
		[Event(name="selectionChanged", type="flash.events.Event")] 
		[Event(name="showDetails", type="com.kaltura.edw.events.GeneralNonCairngormEvent")] 
		[Event(name="moveCategory", type="com.kaltura.edw.events.GeneralNonCairngormEvent")] 
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.kaltura.dataStructures.HashMap;
			import com.kaltura.edw.components.fltr.EntriesFilter;
			import com.kaltura.edw.events.GeneralNonCairngormEvent;
			import com.kaltura.edw.model.types.WindowsStates;
			import com.kaltura.kmc.events.KmcNavigationEvent;
			import com.kaltura.kmc.modules.content.events.CategoryEvent;
			import com.kaltura.kmc.modules.content.events.WindowEvent;
			import com.kaltura.kmc.modules.content.view.content.categories.renderers.NameRenderer;
			import com.kaltura.types.KalturaCategoryOrderBy;
			import com.kaltura.vo.KalturaCategory;
			import com.kaltura.vo.KalturaMediaEntryFilter;
			
			import mx.controls.Alert;
			import mx.events.DataGridEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			
			// ---------------------------------------------
			// Events
			// ---------------------------------------------
			
			
			/**
			 * defines the value of the type property for the <code>orderByChanged</code> event.
			 * */
			public static const ORDER_BY_CHANGED:String = "orderByChanged";
			
			/**
			 * defines the value of the type property for the <code>selectionChanged</code> event.
			 * */
			public static const SELECTION_CHANGED:String = "selectionChanged";
			
			/**
			 * defines the value of the type property for the <code>showDetails</code> event.
			 * */
			public static const SHOW_DETAILS:String = "showDetails";
			
			/**
			 * defines the value of the type property for the <code>moveCategory</code> event.
			 * */
			public static const MOVE_CATEGORY:String = "moveCategory";
			
			
			
			// ---------------------------------------------
			// table sorting
			// ---------------------------------------------
			
			
			private var _orderBy:String = KalturaCategoryOrderBy.CREATED_AT_DESC;
			
			/**
			 * table ordering, optional values listed in <code>KalturaCategoryOrderBy</code>
			 * */
			public function get orderBy():String {
				return _orderBy;
			}
			
			/**
			 * the current sort index
			 * */
			private var _sortIndex:int = 5;
			
			[Bindable]
			/**
			 * the current sort direction
			 * */
			private var _sortDirection:String = "DESC";
			
			private var _columnsSortMap:HashMap = new HashMap();
			
			private function headerReleaseHandler(event:DataGridEvent):void {
				
				event.preventDefault();
				
				if (event.itemRenderer && event.itemRenderer.data && event.itemRenderer.data.headerText) {
					var headerText:String = event.itemRenderer.data.headerText;
					if (!_columnsSortMap.containsKey(headerText)) {
						_columnsSortMap.put(headerText, false);
					}
					
					var sortDir:Boolean = _columnsSortMap.getValue(headerText) as Boolean;
					switch (event.itemRenderer.data.headerText) {
//						case resourceManager.getString('entrytable', 'createAt'):
//							_orderBy = sortDir ? KalturaCategoryOrderBy.CREATED_AT_ASC : KalturaCategoryOrderBy.CREATED_AT_DESC;
//							break;
//						case resourceManager.getString('entrytable', 'idHeader'):
//							_orderBy = sortDir ? KalturaCategoryOrderBy.PLAYS_ASC : KalturaCategoryOrderBy.PLAYS_DESC;
//							break;
						case resourceManager.getString('cms', 'name'):
							_orderBy = sortDir ? KalturaCategoryOrderBy.FULL_NAME_ASC : KalturaCategoryOrderBy.FULL_NAME_DESC;
							break;
						case resourceManager.getString('cms', 'itemCount'):
							_orderBy = sortDir ? KalturaCategoryOrderBy.ENTRIES_COUNT_ASC : KalturaCategoryOrderBy.ENTRIES_COUNT_DESC;
							break;
						default:
							Alert.show(resourceManager.getString('cms', 'cannotOrderByIt', [event.itemRenderer.data.headerText]), resourceManager.getString('cms', 'cannotOrderByItTitle'));
							return;
							break;
					}
					
					// remember the direction and change the sign
					_sortIndex = event.columnIndex;
					_sortDirection = sortDir ? "ASC" : "DESC";
					_columnsSortMap.setValue(headerText, !sortDir);
					setSortIndicator();
					
					dispatchEvent(new Event(ORDER_BY_CHANGED));
				}
			}
			
			
			private function setSortIndicator():void {
				this.mx_internal::sortIndex = _sortIndex;
				this.mx_internal::sortDirection = _sortDirection;
			}

			protected function datagrid1_creationCompleteHandler(event:FlexEvent):void {
				addEventListener(NameRenderer.NAME_CLICKED, openDrilldown, false, 0, true);
				addEventListener(CategoriesTableActions.EDIT, openDrilldown, false, 0, true);
				addEventListener(CategoriesTableActions.DELETE, deleteCategory, false, 0, true);
				addEventListener(CategoriesTableActions.MOVE, moveCategory, false, 0, true);
				addEventListener(CategoriesTableActions.VIEW_ENTRIES, viewEntries, false, 0, true);
			}
			
			
			/**
			 * selection or deselection was made
			 */
			private function setSelection(event:ListEvent):void {
				if (event.itemRenderer.data) {
					var e:Event = new Event(SELECTION_CHANGED);
					dispatchEvent(e);
				}
			}

			
			// ----------------------------------------------
			// table actions
			// ----------------------------------------------
			
			
			private function openDrilldown(e:Event):void {
				// (e.target.data is KalturaCategory)
				var nce:GeneralNonCairngormEvent = new GeneralNonCairngormEvent(CategoriesTable.SHOW_DETAILS);
				nce.data = e.target.data as KalturaCategory;
				dispatchEvent(nce);
			}
			
			private function deleteCategory(e:Event):void {
				var cgEvent:CairngormEvent = new CategoryEvent(CategoryEvent.DELETE_CATEGORIES);
				cgEvent.data = [(e.target.data as KalturaCategory).id];
				cgEvent.dispatch();
			}
			
			private function moveCategory(e:Event):void {
				var nce:GeneralNonCairngormEvent = new GeneralNonCairngormEvent(CategoriesTable.MOVE_CATEGORY);
				nce.data = e.target.data as KalturaCategory;
				dispatchEvent(nce);
			}
			
			private function viewEntries(e:Event):void {
				// set attic.kmef to relevant filter, and move to entries screen
				var kmef:KalturaMediaEntryFilter = EntriesFilter.generateBaseFilter();
				kmef.categoriesIdsMatchOr = (e.target.data as KalturaCategory).id.toString();
				
				dispatchEvent(new KmcNavigationEvent(KmcNavigationEvent.NAVIGATE, "content", "manage", {kmef: kmef}));
			}

		]]>
	</mx:Script>
	<mx:columns>
		<mx:DataGridColumn id="cid" paddingLeft="4" paddingRight="4" dataField="id" 
						   width="90" headerText="{resourceManager.getString('cms', 'idHeader')}" 
						   itemRenderer="com.kaltura.edw.components.et.ir.GeneralRenderer"/>
		
		<mx:DataGridColumn id="cname" paddingLeft="4" paddingRight="4" dataField="name" 
						   headerText="{resourceManager.getString('cms', 'name')}" 
						   itemRenderer="com.kaltura.kmc.modules.content.view.content.categories.renderers.NameRenderer"/>
		
		<mx:DataGridColumn id="items" paddingLeft="4" paddingRight="4" dataField="directEntriesCount" 
						   headerText="{resourceManager.getString('cms', 'itemCount')}" 
						   itemRenderer="com.kaltura.edw.components.et.ir.GeneralRenderer"/>
		
		<mx:DataGridColumn id="actions" paddingLeft="4" paddingRight="4" width="140"
						   headerText="{resourceManager.getString('cms', 'actions')}"
						   itemRenderer="com.kaltura.kmc.modules.content.view.content.categories.renderers.ActionsRenderer"/>
	</mx:columns>
</mx:DataGrid>
