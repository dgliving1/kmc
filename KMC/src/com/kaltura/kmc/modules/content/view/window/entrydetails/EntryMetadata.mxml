<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:utils="com.kaltura.kmc.modules.content.utils.*"
		 creationComplete="onCreationComplete()" implements="com.kaltura.kmc.modules.content.business.IDrilldownPanel">
	<mx:Metadata>
		[Event(name="untrackableChange", type="flash.events.Event")] 
		[Event(name="openPreview", type="flash.events.Event")] 
	</mx:Metadata>


	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.kaltura.KalturaClient;
			import com.kaltura.analytics.GoogleAnalyticsConsts;
			import com.kaltura.analytics.GoogleAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTrackerConsts;
			import com.kaltura.events.KdpEventTypes;
			import com.kaltura.kmc.business.JSGate;
			import com.kaltura.kmc.modules.content.events.EntryEvent;
			import com.kaltura.kmc.modules.content.events.ModelEvent;
			import com.kaltura.kmc.modules.content.events.UploadEntryEvent;
			import com.kaltura.kmc.modules.content.model.Context;
			import com.kaltura.kmc.modules.content.model.EntryDetailsModel;
			import com.kaltura.kmc.modules.content.vo.EntryDetailsValidationError;
			import com.kaltura.kmc.modules.content.vo.FlavorAssetWithParamsVO;
			import com.kaltura.types.KalturaEditorType;
			import com.kaltura.types.KalturaEntryModerationStatus;
			import com.kaltura.types.KalturaEntryStatus;
			import com.kaltura.types.KalturaEntryType;
			import com.kaltura.types.KalturaMediaType;
			import com.kaltura.types.KalturaStatsKmcEventType;
			import com.kaltura.utils.KTimeUtil;
			import com.kaltura.vo.KalturaBaseEntry;
			import com.kaltura.vo.KalturaFlavorAsset;
			import com.kaltura.vo.KalturaFlavorAssetWithParams;
			import com.kaltura.vo.KalturaLiveStreamAdminEntry;
			import com.kaltura.vo.KalturaMediaEntry;
			import com.kaltura.vo.KalturaMixEntry;
			
			import flash.utils.getQualifiedClassName;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.containers.ViewStack;
			import mx.controls.Image;
			import mx.core.Container;
			import mx.events.ResizeEvent;
			import mx.formatters.DateFormatter;
			import mx.resources.ResourceManager;
			import mx.utils.StringUtil;

			public static const UNTRACKABLE_CHANGE:String = "untrackableChange";
			public static const OPEN_PREVIEW:String = "openPreview";

			private const ENTRY_PLACEHOLDER:String = '{entryId}';

			/**
			 * landing page according to partner's data
			 * */
			public var landingPage:String;

			/**
			 * default image to display when entry contains no content
			 * */
			private static const noMediaImage:* = StyleManager.getStyleDeclaration(".imageBank").getStyle("noContentImg");

			/**
			 * reference to kdp3Loader.
			 * due to performance issue that we fixed, this is static.
			 * */
			private static var kdp3StaticSwfLoader:SWFLoader;


			private const LABEL_WIDTH:Number = 105;
			private const TEXT_WIDTH:Number = 320;

			[Bindable]
			public var selectedEntry:KalturaBaseEntry;

			[Bindable]
			/**
			 * indicates the current entry is a livestream entry
			 * */
			public var isLiveStream:Boolean;

			[Bindable]
			public var context:Context;

			[Bindable]
			public var entryDetailsModel:EntryDetailsModel;

			private var _maxNumCategories:int;

			private var _fr:FileReference;

			private var _changedThumbnail:Boolean;

			/**
			 * entry thumbnail for image entries
			 * */
			private var _entryImg:Image;


			[Bindable]
			private var _entryHasContent:Boolean;
			
			private var _selectedEntryId:String;

			private var _originalRefid:String;
			
			[Bindable]
			/**
			 * RnP: enable trimming button
			 * */
			public var allowTrimming:Boolean = true;
			
			[Bindable]
			/**
			 * RnP: enable clipping button
			 * */
			public var allowClipping:Boolean = true;
			
			
			/**
			 * for trip / clip buttons, decide if the button should be
			 * enabled according to whether entry has source flavor.
			 * @param flavors	the list of flavors associated with this entry
			 * @return whether the button should be enabled or not
			 * */
			private function entryHasSource(flavors:ArrayCollection):Boolean {
				var hasSource:Boolean = false;
				var asset:KalturaFlavorAsset;
				for each (var vo:FlavorAssetWithParamsVO in flavors) {
					asset = vo.kalturaFlavorAssetWithParams.flavorAsset; 
					if (asset && asset.isOriginal) {
						hasSource = true;
						break;
					}
				}
				return hasSource;
			}
			

			public function initData():void {
				//reset values to starting point
				_entryHasContent = true;
				_originalRefid = selectedEntry.referenceId;
				if (kdpContainer.getChildAt(0) is Image)
					kdpContainer.removeChildAt(0);
				
				loaderContainer.includeInLayout = loaderContainer.visible = true;
				
				if (selectedEntry.status == KalturaEntryStatus.NO_CONTENT) {
					addImageThumb(noMediaImage);
					_entryHasContent = false;
				}
				else if ((selectedEntry is KalturaMediaEntry) && 
					(selectedEntry as KalturaMediaEntry).mediaType == KalturaMediaType.IMAGE) {
					addImageThumb();
				}
				else {
					//mix or video
					loadKDP();
				}	
				//redundant: we already got this info from the filter
				//var listCategories:CategoryEvent = new CategoryEvent(CategoryEvent.LIST_CATEGORIES);
				//listCategories.dispatch();
			}


			public function destroy():void {

			}


//			public function get viewStack():ViewStack {
//				return _viewStack;
//			}
//
//
//			public function set viewStack(value:ViewStack):void {
//				_viewStack = value;
//			}


			/**
			 * remove any playing entry from the preview player
			 * */
			public function clearPlayer():void {
				if (kdp3StaticSwfLoader && kdp3StaticSwfLoader.content) {
					kdp3StaticSwfLoader.content['sendNotification'](KdpEventTypes.DO_STOP);
					kdp3StaticSwfLoader.content['sendNotification'](KdpEventTypes.CLEAN_MEDIA); //clear the entry from the kdp
				}
			}


			/**
			 * see all data on the panel is legal and save data
			 * @return an object with error code
			 * */
			public function save():EntryDetailsValidationError {
				var result:EntryDetailsValidationError = new EntryDetailsValidationError();
				// entry name
				if (name_input.text == "") {
					result.error = EntryDetailsValidationError.ENTRY_NAME_MISSING;
				}
				if (result.error == null) {
					// categories section
					// auto-complete event isn't full so we need to add this here
					var catArr:Array = categoriesTextInput.text.split(',');
					var tempArr:Array = [];
					for each (var cat:String in catArr) {
						var sCat:String = StringUtil.trim(cat);
						if (sCat != '') {
							tempArr.push(sCat);
						}
					}
					if (tempArr.length > maxNumCategories) {
						result.error = EntryDetailsValidationError.CATEGORIES_LIMIT;
					}
					else {
						// set the categories on the entry
						selectedEntry.categories = tempArr.join(',');
					}
				}
//				if (!selectedEntry.referenceId) {
//					// so we'll delete older values
//					selectedEntry.referenceId = KalturaClient.NULL_STRING;
//				}
				if (isLiveStream)
					(selectedEntry as KalturaLiveStreamAdminEntry).offlineMessage = offlineMessage.text;
				return result;
			}



			/**
			 * Format the creation date
			 */
			private function formatDate(date:Number):String {
				var df:DateFormatter = new DateFormatter();
				df.formatString = resourceManager.getString('drilldown', 'drilldowndateformat');
				var dt:Date = new Date();
				dt.setTime(date * 1000);
				return df.format(dt);
			};


			/*	private function displayThumbnailOption(value:Boolean):void {
			   thumbnailOptionBox.visible = thumbnailOptionBox.includeInLayout = value;
			 }*/

			/**
			 * this can't be done on creationComplete because selectedEntry is
			 * only set at the parent's creationComplete, which comes later. so
			 * the parent has to manualy initialize this panel.
			 * */
			public function init():void {
				// bind to the REAL entry, not the copy, so the thumbnail  
				// image will be updated when a new thumbnail is uploaded
				BindingUtils.bindSetter(updateUndoEntryThumbnail, entryDetailsModel.selectedEntry, "thumbnailUrl");

				// ===========================================================

				/*var showThumbnailUi:Boolean = false;
				   if (selectedEntry is KalturaMediaEntry &&
				   ((selectedEntry as KalturaMediaEntry).mediaType != KalturaMediaType.IMAGE)) {
				   showThumbnailUi = true;
				   }
				   else {
				   showThumbnailUi = false;
				   }
				   if (selectedEntry is KalturaMixEntry) {
				   showThumbnailUi = true;
				   }

				 displayThumbnailOption(showThumbnailUi);		*/

				// ===========================================================

			

				if (landingPage && landingPage != '') {
					var replaceIndex:int = landingPage.indexOf(ENTRY_PLACEHOLDER);
					if (replaceIndex > -1)
						landingPageLabel.text = landingPage.replace(ENTRY_PLACEHOLDER, selectedEntry.id);
					else
						landingPageLabel.text = landingPage + selectedEntry.id;
				}
			}


			protected function onCreationComplete():void {
				name_input.setFocus();
			}


			/**
			 * Load KDP.
			 * if we already have a loaded KDP, we add it to the parent of the new (instance) kdploader
			 * instead of loading a new kdp.
			 */
			private function loadKDP():void {
				//if this is the second time we open the content
				if (kdp3StaticSwfLoader && kdp3StaticSwfLoader.content) {
					// parent of instance loader
					var kdp3LoaderParent:DisplayObjectContainer = kdp3Loader.parent;
					// add the static loader instead of the instance loader: (if kdp3Loader has content it means we are in the same 
					// drill down session)
					if (kdp3LoaderParent && !kdp3Loader.content) {
						kdp3LoaderParent.addChildAt(kdp3StaticSwfLoader, kdp3LoaderParent.getChildIndex(kdp3Loader));
						kdp3LoaderParent.removeChild(kdp3Loader);
					}
					// listen to kdp ready to show it because on the second time if we won't do that we will see the prev thumb
					// because this is static KDP (due to performance issue)
					kdp3StaticSwfLoader.visible = false;
					kdp3StaticSwfLoader.content.addEventListener(KdpEventTypes.ENTRY_READY, showKDP);
					kdp3StaticSwfLoader.content["sendNotification"](KdpEventTypes.CHANGE_MEDIA, {entryId: entryDetailsModel.selectedEntry.id});
				}
				else {
					var newUrl:String = "http://" + context.rootUrl + "/kwidget/wid/_" + context.kc.partnerId + "/ui_conf_id/" + context.drilldownUiconf + "/nowrapper/1";
					kdp3Loader.scaleContent = false;
					kdp3Loader.loaderContext = new LoaderContext(true, new ApplicationDomain());
					kdp3Loader.load(newUrl);
				}
			}

			private function stopEvent(e:Event):void {
				var cl:String = getQualifiedClassName(e);
				if (e.type == "resize" &&  cl == "fl.events::ComponentEvent") {
					e.stopImmediatePropagation();
					e.preventDefault();
				}
			}

			private function showKDP(event:Event = null):void {
				kdp3StaticSwfLoader.visible = true;
				kdpContainer.validateNow();
//				kdp3StaticSwfLoader.content["focusManager"].deactivate();
			}


			private function onStandardClick():void {
				mix(selectedEntry.mediaType, selectedEntry.type, KalturaEditorType.SIMPLE);
				deleayEditors();
				pausePreview();
			}


			private function onAdvancedEditor():void {
				mix(selectedEntry.mediaType, selectedEntry.type, KalturaEditorType.ADVANCED);
				deleayEditors();
				pausePreview();
			}


			/**
			 * save and load new image
			 * */
			private function updateUndoEntryThumbnail(thumbnailUrl:String):void {
				if (selectedEntry) {
					selectedEntry.thumbnailUrl = thumbnailUrl;
				}
			}


			/**
			 * pause the preview player
			 * */
			public function pausePreview():void {
				if (kdp3StaticSwfLoader && kdp3StaticSwfLoader.content) {
					kdp3StaticSwfLoader.content['sendNotification'](KdpEventTypes.DO_PAUSE);
				}
			}


			private function setFile(event:Event):void {
				_changedThumbnail = true;
				var cgEvent:UploadEntryEvent = new UploadEntryEvent(UploadEntryEvent.UPLOAD_THUMBNAIL, selectedEntry.id, _fr);
				cgEvent.dispatch();

				KAnalyticsTracker.getInstance().sendEvent(KAnalyticsTrackerConsts.CONTENT, KalturaStatsKmcEventType.CONTENT_CHANGE_THUMBNAIL, "contentChangeThumbnail", selectedEntry.id);
				GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.CONTENT_CHANGE_THUMBNAIL, GoogleAnalyticsConsts.CONTENT);
			}



			private function addImageThumb(imgSource:Object = null):void {
				loaderContainer.includeInLayout = loaderContainer.visible = false;
				_entryImg = new Image();
				_entryImg.width = 300;
				_entryImg.height = 255;
				_entryImg.maintainAspectRatio = true;
				_entryImg.setStyle("horizontalAlign", "center");
				_entryImg.setStyle("verticalAlign", "middle");
				if (imgSource)
					_entryImg.source = imgSource;
				else
					_entryImg.source = selectedEntry.thumbnailUrl + "/width/240/height/180/bgcolor/F7F7F7/type/2";
				kdpContainer.addChildAt(_entryImg, 0);
			}



			/**
			 * track thumbnail change
			 * */
			private function onThumbnailSaved():void {
				GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.CONTENT_CHANGE_THUMBNAIL, GoogleAnalyticsConsts.CONTENT);
				KAnalyticsTracker.getInstance().sendEvent(KAnalyticsTrackerConsts.CONTENT, KalturaStatsKmcEventType.CONTENT_CHANGE_THUMBNAIL, "KDP3>DrillDown>Change Thumbnail");
			}


			/**
			 * Check if this entry is a mix and if it was made in advanced editor
			 * if it is
			 */
			private function checkIfNotAdvanced(undoToEntry:*):Boolean {
				// editorType.toString();
				if (((undoToEntry is KalturaMixEntry) && (undoToEntry as KalturaMixEntry).editorType == KalturaEditorType.ADVANCED) 
					|| selectedEntry.status != KalturaEntryStatus.READY)
					return false;
				return true;
			}


			/**
			 * Mix action - create a mix from this entry or open editor
			 */
			private function mix(mediaType:String, type:String, editorType:int):void {
				dispatchEvent(new Event(EntryMetadata.UNTRACKABLE_CHANGE));
				if (type == KalturaEntryType.MIX) {
					//this is a mix
					var mixEditorType:String = (selectedEntry as KalturaMixEntry).editorType.toString();
					// arguments: entryId, name , editorType , isNewMix (true: create a mix and then
					// open an editor, false - edit this mix with the matching editor type)
					JSGate.startEditor(selectedEntry.id, selectedEntry.name, editorType, false);
				}
				switch (mediaType) {
					//this is an entry (video image or audio)
					case "1":
					case "2":
					case "5":
						// arguments: entryId, name , editorType , isNewMix (true: create a mix and then open an editor, 
						//false - edit this mix with the matching editor type)
						JSGate.startEditor(selectedEntry.id, selectedEntry.name, editorType, true);
						break;
				}

			}


			private function openPreview():void {
				//stop the player from playing 
				if (kdp3StaticSwfLoader) {
					kdp3StaticSwfLoader.content['sendNotification'](KdpEventTypes.DO_STOP);
				}
				dispatchEvent(new Event(EntryMetadata.OPEN_PREVIEW));
			}


			private function uncaughtErrorHandler(event:Event):void {
				event.preventDefault();
				trace(event, event["error"]);
			}


			/**
			 * Kdp loaded, initialize it.
			 */
			private function onKDP3Loaded(event:Event):void {
				kdp3Loader.tabChildren = false;
				kdp3Loader.tabEnabled = false;
				name_input.setFocus();
				kdp3StaticSwfLoader = kdp3Loader;
				systemManager.addEventListener(ResizeEvent.RESIZE, stopEvent, false, 1);
				if (kdp3Loader.loaderInfo.hasOwnProperty("uncaughtErrorEvents"))
					IEventDispatcher(kdp3Loader.loaderInfo["uncaughtErrorEvents"]).addEventListener("uncaughtError", uncaughtErrorHandler, false, 1, true);

				//set kdp params
				var params:Object = new Object();
				params.widgetId = "_" + context.kc.partnerId;
				params.cdnHost = context.cdnHost;
				params.host = context.rootUrl;
				params.autoPlay = "false";
				params.loop = "false";
				params.autoRewind = "false";
				params.sourceType = "entryId";
				params.entryId = entryDetailsModel.selectedEntry.id;
//				if (context.rootUrl)
//					params.host = context.rootUrl;
				if (context.drilldownUiconf)
					params.uiConfId = context.drilldownUiconf;
				params.ks = context.kc.ks;
				params.partnerId = context.kc.partnerId;
				params.subpId = context.kc.partnerId + "00";

//				params.cdnUrl = context.rootUrl;
				params.debugMode = context.debugMode;

				kdp3StaticSwfLoader.content["flashvars"] = params;
				//start the loading sqeunce of the kdp	
				kdp3StaticSwfLoader.content["init"]();
				kdp3StaticSwfLoader.addEventListener(KdpEventTypes.THUMBNAIL_SAVED, onThumbnailSaved)
			}


//			/**
//			 * return a matching value - to edit current mix or create a new mix from this entry
//			 */
//			private function labelMix(mediaType:String, type:String):String {
//				if (type == KalturaEntryType.MIX) {
//					return resourceManager.getString('drilldown', 'editmix');
//				}
//				switch (mediaType) {
//					case "1":
//					case "2":
//					case "5":
//						return resourceManager.getString('drilldown', 'createmixfromentry');
//						break;
//				}
//
//				return "";
//			}


			private function getModeration(moderationCode:int):String {
				switch (moderationCode) {
					case KalturaEntryModerationStatus.APPROVED:  {
						return resourceManager.getString('drilldown', 'approved');
					}
					case KalturaEntryModerationStatus.FLAGGED_FOR_REVIEW:  {
						return resourceManager.getString('drilldown', 'pending');
					}
					case KalturaEntryModerationStatus.REJECTED:  {
						return resourceManager.getString('drilldown', 'rejected');
					}
					case KalturaEntryModerationStatus.AUTO_APPROVED:  {
						return resourceManager.getString('drilldown', 'autoApproved');
					}
					case KalturaEntryModerationStatus.PENDING_MODERATION:  {
						return resourceManager.getString('drilldown', 'pendingModeration');
					}

					default:  {
						return ' -- ';
					}
				}
			}


			/**
			 * The function translate media type enum to the matching locale string
			 * @param mediaType
			 * @param type - special param for mix since mix is type 2 and other types are type 1 with different mediaTypes
			 */
			private function getMediaTypes(mediaType:int, type:String):String {
				if (type == KalturaEntryType.MIX) {
					return resourceManager.getString('drilldown', 'videoMix');
				}
				switch (mediaType) {
					case KalturaMediaType.VIDEO:
						return resourceManager.getString('drilldown', 'video');
						break;
					case KalturaMediaType.IMAGE:
						return resourceManager.getString('drilldown', 'image');
						break;
					case KalturaMediaType.AUDIO:
						return resourceManager.getString('drilldown', 'audio');
						break;
					case "6":
						return resourceManager.getString('drilldown', 'videoMix');
						break;
					case "10":
						return resourceManager.getString('drilldown', 'xml');
						break;
					case KalturaMediaType.LIVE_STREAM_FLASH:
						return resourceManager.getString('drilldown', 'liveStream');
						break;
				}

				return "";
			}



			[Bindable]
			private var _simpleEditorTimer:Boolean = true;
			[Bindable]
			private var _advancedEditorTimer:Boolean = true;

			[Bindable]
			private var _showEditorLinks:Boolean = true;
			[Bindable]
			private var _simpleEditorEnable:Boolean = true;
			[Bindable]
			private var _advancedEditorEnable:Boolean = true;
			[Bindable]
			private var _previewEnable:Boolean = true;
			[Bindable]
			/**
			 * @copy #previewOnly
			 * */
			private var _showEmbed:Boolean = false;


			/**
			 * workaround for boolean and in mxml binding expressions
			 * @param val1 boolean expression
			 * @param val2 boolean expression
			 * @return boolean value of val1 && val2
			 * */
			protected function booleanAnd(val1:Boolean, val2:Boolean):Boolean {
				return val1 && val2;
			}


			public function get simpleEditorEnabled():Boolean {
				return _simpleEditorEnable;
			}


			public function set simpleEditorEnabled(value:Boolean):void {
				_simpleEditorEnable = value;
			}


			public function get advancedEditorEnabled():Boolean {
				return _advancedEditorEnable;
			}


			public function set advancedEditorEnabled(value:Boolean):void {
				_advancedEditorEnable = value;
			}


			public function get previewEnable():Boolean {
				return _previewEnable;
			}


			public function set previewEnable(value:Boolean):void {
				_previewEnable = value;
			}
			[Bindable]
			public var previewLabel:String = ResourceManager.getInstance().getString('drilldown', 'previewAndEmbed');


			/**
			 * show only "preview" and not "preview & embed"
			 */
			public function set showEmbed(value:Boolean):void {
				if (value)
					previewLabel = resourceManager.getString('drilldown', 'previewAndEmbed');
				else {
					previewLabel = resourceManager.getString('drilldown', 'previewOnly');
				}
				_showEmbed = value;
			}


			public function get showEmbed():Boolean {
				return _showEmbed;
			}


			/**
			 * This function prevents the user from re-click the editors for 5 sec because it takse time to open
			 * the HTML editors and users re-click the editors.
			 */
			protected function deleayEditors():void {
				var t:Timer = new Timer(5000, 1);
				t.addEventListener(TimerEvent.TIMER_COMPLETE, onDelayDone);
				_simpleEditorTimer = false;
				_advancedEditorTimer = false;
				t.start();
			}


			/**
			 * 5 sec over - return the ability to the editor buttons
			 */
			protected function onDelayDone(event:TimerEvent):void {
				event.target.removeEventListener(TimerEvent.TIMER_COMPLETE, onDelayDone);
				_simpleEditorTimer = true;
				_advancedEditorTimer = true;
			}


//			/**
//			 * will redirect the user to "thumbnails" tab
//			 * */
//			private function onChangeThumbnailClick(event:MouseEvent):void {
//				_viewStack.selectedChild = _viewStack.getChildByName("entryThumbnails") as Container;
//			}


			/**
			 * show the editor links box
			 * */
			public function get showEditorLinks():Boolean {
				return _showEditorLinks;
			}


			public function set showEditorLinks(value:Boolean):void {
				_showEditorLinks = value;
			}


			public function get maxNumCategories():int {
				return _maxNumCategories;
			}


			public function set maxNumCategories(value:int):void {
				_maxNumCategories = value;
			}

			/**
			 * open drilldown window wit the parent entry
			 * (the entry this clip was made from)
			 * */
			protected function showParentEntry(event:MouseEvent):void
			{
				// create the new model 
				var cg:CairngormEvent = new ModelEvent(ModelEvent.DUPLICATE_ENTRY_DETAILS_MODEL);
				cg.dispatch();
				cg = new EntryEvent(EntryEvent.GET_ENTRY_AND_DRILLDOWN, null, selectedEntry.rootEntryId);
				cg.dispatch();
			}
			
			/**
			 * request JS to open the clipApp in clipping mode
			 * */
			protected function openClipping(event:MouseEvent):void {
				pausePreview();
				JSGate.openClipApp(selectedEntry.id, "clip");
			}
			
			/**
			 * request JS to open the clipApp in trimming mode
			 * */
			protected function openTrimming(event:MouseEvent):void {
				pausePreview();
				JSGate.openClipApp(selectedEntry.id, "trim");
			}
			
			private function getRefid(refid:String):String {
				if (refid == KalturaClient.NULL_STRING) {
					return '';
				}
				else return refid;
			}


			protected function referenceId_changeHandler(event:Event):void
			{
//				selectedEntry.referenceId = event.target.text;
				if(event.target.text == '') {
					if (_originalRefid == null){
						// no change from original empty value
						selectedEntry.referenceId = null;
					}
					else {
						// used to have value, delete it
						selectedEntry.referenceId = KalturaClient.NULL_STRING;
					}
				}
				else {
					selectedEntry.referenceId = event.target.text;
				}
			}

		]]>
	</mx:Script>
	<mx:StringValidator id='nameListValidator' source="{name_input}" property="text" triggerEvent="change"
						required="true" requiredFieldError="{resourceManager.getString('drilldown', 'entryNameIsMandatory')}"/>
	<mx:VBox width="100%" verticalGap="3">
		<mx:HBox width="100%">
			<mx:Label text="{resourceManager.getString('drilldown','name')}:" width="{LABEL_WIDTH}" styleName="drillDownLabel"/>
			<mx:TextInput id="name_input" width="{TEXT_WIDTH}" text="{selectedEntry.name}"
						  change="{selectedEntry.name = event.target.text}" styleName="drillDownSubLabel"/>
		</mx:HBox>
		<mx:HBox width="100%">
			<mx:Label text="{resourceManager.getString('drilldown','description')}:" width="{LABEL_WIDTH}"
					  styleName="drillDownLabel"/>
			<mx:TextArea id="descriptionTi" width="{TEXT_WIDTH}" text="{selectedEntry.description}" 
						 change="{selectedEntry.description = event.target.text}" styleName="drillDownSubLabel"/>
		</mx:HBox>
		<mx:HBox width="100%">
			<mx:Label id="tagsLbl" text="{resourceManager.getString('drilldown','tags')}:" width="{LABEL_WIDTH}"
					  styleName="drillDownLabel"/> 
			<mx:TextInput id="tagsTi" width="{TEXT_WIDTH}" text="{selectedEntry.tags}" 
						  change="{selectedEntry.tags = event.target.text}" styleName="drillDownSubLabel"/>
		</mx:HBox>
		<mx:HBox width="100%">
			<mx:Label text="{resourceManager.getString('drilldown','drillDownCategories')}:" width="{LABEL_WIDTH}"
					  styleName="drillDownLabel"/>
			<utils:AutoComplete id='categoriesTextInput' width="{TEXT_WIDTH}" height="{tagsTi.height}" 
								text="{selectedEntry.categories}" styleName="autoComplete"
								dataProvider="{entryDetailsModel.categoriesFullNameList}" />
		</mx:HBox>
		<!--<mx:VBox styleName="lineGap">-->
			<mx:HBox width="100%" visible="{isLiveStream}" includeInLayout="{isLiveStream}">
				<mx:Label text="{resourceManager.getString('drilldown','offlineMessage')}" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:TextArea id="offlineMessage" width="{TEXT_WIDTH}" text="{selectedEntry.offlineMessage}"
							 styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label htmlText="{resourceManager.getString('drilldown','referenceId')}" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:TextInput id="referenceId" width="{TEXT_WIDTH}" text="{getRefid(selectedEntry.referenceId)}"
							  change="referenceId_changeHandler(event)" styleName="drillDownSubLabel"/>
			</mx:HBox>

			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('drilldown','entryId')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:Label text="{selectedEntry.id}" selectable="true" styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('drilldown','landingPage')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:Label id="landingPageLabel" selectable="true" styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('drilldown','type')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:Label text="{getMediaTypes(int(selectedEntry.mediaType),selectedEntry.type)}"
						  styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('drilldown','moderation')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:Label text="{getModeration(selectedEntry.moderationStatus)}" styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('drilldown','flags')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:Label text="{selectedEntry.flags}" styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('drilldown','duration')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:Label text="{KTimeUtil.formatTime(selectedEntry.duration)}" styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('drilldown','createdDate')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:Label text="{formatDate(selectedEntry.createdAt)}" styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('drilldown','creator')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:Label text="{selectedEntry.userId}" styleName="drillDownSubLabel"/>
			</mx:HBox>
			<mx:HBox width="100%" visible="{selectedEntry.rootEntryId != selectedEntry.id}" includeInLayout="{selectedEntry.rootEntryId != selectedEntry.id}">
				<mx:Label text="{resourceManager.getString('drilldown','clippedFrom')}:" width="{LABEL_WIDTH}"
						  styleName="drillDownLabel"/>
				<mx:LinkButton label="{selectedEntry.rootEntryId}" click="showParentEntry(event)"/>
			</mx:HBox>
		<!--</mx:VBox>-->
	</mx:VBox>
	<mx:VBox id="kdpContainer" width="100%" height="100%" styleName="lineGap" horizontalAlign="center">
		<mx:HBox width="100%" paddingTop="5">
			<mx:VBox width="100%" horizontalAlign="center">
				<mx:HBox id="loaderContainer" width="100%" paddingLeft="0" >
					<mx:SWFLoader id="kdp3Loader" width="300" height="255" complete="{onKDP3Loaded(event)}"/>
				</mx:HBox>
				<mx:VBox visible="{_entryHasContent}" includeInLayout="{_entryHasContent}" horizontalAlign="center">
					<mx:Button id="previewAndEmbed" label="{previewLabel}" buttonMode="{previewAndEmbed.enabled}"
							   click="{openPreview()}" width="135" styleName="dataGridBtn"
							   enabled="{booleanAnd(selectedEntry.status == KalturaEntryStatus.READY,_previewEnable)}"/>

					<mx:HBox width="100%" id="editorsBox" horizontalAlign="center" includeInLayout="{editorsBox.visible}" 
							 visible="{(selectedEntry as KalturaMediaEntry).mediaType != KalturaMediaType.IMAGE &amp;&amp; selectedEntry.status == KalturaEntryStatus.READY}" >
						<mx:LinkButton id="btnClipping" click="openClipping(event)" enabled="{entryHasSource(entryDetailsModel.flavorParamsAndAssetsByEntryId)}"
									   label="{resourceManager.getString('drilldown','openClipping')}" />
						<mx:LinkButton id="btnTrimming" click="openTrimming(event)" enabled="{entryHasSource(entryDetailsModel.flavorParamsAndAssetsByEntryId)}"
									   label="{resourceManager.getString('drilldown','openTrimming')}" />
					</mx:HBox>
					<mx:HBox width="100%">
						<mx:Label text="{resourceManager.getString('drilldown','rating')}:" styleName="drillDownLabel"/>
						<mx:Label text="{selectedEntry.rank}" styleName="drillDownSubLabel"/>

						<mx:Label text="{resourceManager.getString('drilldown','votesCount')}:" styleName="drillDownLabel"/>
						<mx:Label text="{selectedEntry.votes}" styleName="drillDownSubLabel"/>
						
					</mx:HBox>
					<mx:HBox width="100%">
						<mx:Label text="{resourceManager.getString('drilldown','plays')}:" styleName="drillDownLabel"/>
						<mx:Label text="{selectedEntry.plays}" styleName="drillDownSubLabel"/>
					</mx:HBox>
				</mx:VBox>
				<mx:Label visible="{!_entryHasContent}" includeInLayout="{!_entryHasContent}"
						  text="{resourceManager.getString('drilldown','noContentMessage')}" styleName="noContentLabel"/>
			</mx:VBox>
		</mx:HBox>
	</mx:VBox>
</mx:HBox>
