<?xml version="1.0" encoding="utf-8"?>
<containers:HelpTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
							xmlns:containers="com.kaltura.containers.*" 
							layout="vertical" width="400" height="300"
							showCloseButton="true" showHelpButton="true" help="helpHandler(event)"
							title="{_windowTitle}" xmlns:cat="com.kaltura.edw.components.fltr.cat.*">
	<mx:Script>
		<![CDATA[
			import com.kaltura.KalturaClient;
			import com.kaltura.edw.components.fltr.FilterComponentEvent;
			import com.kaltura.edw.components.fltr.cat.CatTreeSelectionMode;
			import com.kaltura.edw.model.FilterModel;
			import com.kaltura.edw.vo.CategoryVO;
			import com.kaltura.kmc.events.KmcHelpEvent;
			import com.kaltura.vo.KalturaCategory;
			
			import mx.core.mx_internal;
			
			import org.hamcrest.collection.array;
			
			[Bindable]
			private var _windowTitle:String;
			
			[Bindable]
			private var _descriptionTxt:String;
			
			
			[Bindable]
			public var filterModel:FilterModel;
			
			
			[Bindable]
			/**
			 * container variable for affected categories list
			 * */
			private var _categories:Array;
			
			/**
			 * list of categories to move
			 * KalturaCategory objects
			 * */
			public function setCategories(value:Array):void {
				_categories = value;
				if (value.length == 1) {
					_windowTitle = resourceManager.getString('cms', 'moveSingleCategoryTitle');
					_descriptionTxt = resourceManager.getString('cms', 'moveSingleCategoryText');
				}
				else {
					_windowTitle = resourceManager.getString('cms', 'moveMultiCategoriesTitle');
					_descriptionTxt = resourceManager.getString('cms', 'moveMultiCategoriesText');
				}
			}
			
			/**
			 * retrieve a list of affected categories
			 * */
			public function getCategories():Array {
				return _categories;
			}
			
			/**
			 * get new parent category
			 * */
			public function getParent():KalturaCategory {
				if (none.selected) {
					// return the "root"
					var kCat:KalturaCategory = new KalturaCategory();
					kCat.id = 0;
					return kCat;
				}
				return (catTree.getSelectedCategories()[0] as CategoryVO).category;
			}
			
			protected function button1_clickHandler(event:MouseEvent):void {
				dispatchEvent(new Event("apply"));
				mx_internal::closeButton.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
			}
			

			private function helpHandler(event:Event):void {
				dispatchEvent(new KmcHelpEvent(KmcHelpEvent.HELP, "section_cat_move", true));
			}


			protected function catTree_valueChangeHandler(event:FilterComponentEvent):void {
				none.selected = false;
			}


			protected function none_clickHandler(event:MouseEvent):void {
				catTree.clearSelection();
			}

		]]>
	</mx:Script>
	<mx:Text text="{_descriptionTxt}" width="100%"/>
	<mx:RadioButton id="none" label="{resourceManager.getString('cms', 'noParentLbl')}" selected="true" click="none_clickHandler(event)"/>
	<mx:HRule width="90%" />
	<cat:CatTree id="catTree" styleName="categoriesTree" width="100%" height="100%" 
				 selectionMode="{CatTreeSelectionMode.SINGLE_SELECT}"
				 chunkedData="{filterModel.chunkedCategoriesLoad}"
				 categories="{filterModel.categoriesMapGeneral}"
				 dataProvider="{filterModel.categoriesGeneral}" 
				 disabledCategories="{_categories}"
				 valueChange="catTree_valueChangeHandler(event)"/>
	<mx:ControlBar>
		<mx:Button label="{resourceManager.getString('cms', 'apply')}" click="button1_clickHandler(event)" />
	</mx:ControlBar>
</containers:HelpTitleWindow>
