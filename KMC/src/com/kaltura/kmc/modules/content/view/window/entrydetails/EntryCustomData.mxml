<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" 
		 implements="com.kaltura.kmc.modules.content.business.IDrilldownPanel"
		 verticalGap="{customDataVerticalGap}" >
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.content.events.MetadataDataEvent;
			import com.kaltura.kmc.modules.content.utils.FormBuilder;
			import com.kaltura.kmc.modules.content.view.window.entrydetails.customDataComponents.SingleCustomData;
			import com.kaltura.utils.pager.Pager;
			import com.kaltura.vo.KMCMetadataProfileVO;
			
			import mx.collections.ArrayCollection;
			
			private const customDataVerticalGap:Number = 5;
			
			
			[Bindable]
			/**
			 * names of custom data profiles
			 * */
			private var _profileNames:ArrayCollection;
			
			public function destroy():void {
				var curCustomData:SingleCustomData;
				for (var i:int = profilesVBox.numChildren-1; i>=0; i--) {
					curCustomData = profilesVBox.getChildAt(i) as SingleCustomData; 
					curCustomData.removeEventListener(SingleCustomData.SCROLL_TO_TOP, scrollToTop);
					curCustomData.destroy();
				}
			}
			
			/**
			 * loads required data from server
			 * */
			public function initData():void {
				var listCustomData:MetadataDataEvent = new MetadataDataEvent(MetadataDataEvent.LIST)
				listCustomData.dispatch();
			}
			
			
			/**
			 * create the visual representation of each profile
			 * @param profilesAC list of custom data profiles (<code>KMCMetadataProfileVO</code>)
			 * @param formBuildersAC	list of matching form builders (<code>FormBuilder</code>)
			 * */
			public function buildProfiles(profilesAC:ArrayCollection, formBuildersAC:ArrayCollection):void {
				//remove old data and old binding
				destroy();
				profilesVBox.removeAllChildren();
						
				_profileNames = new ArrayCollection();
				
				for (var i:int = 0; i<profilesAC.length; i++) {					
					var curProfile:KMCMetadataProfileVO = profilesAC.getItemAt(i) as KMCMetadataProfileVO;
					
					if (curProfile.profile && curProfile.metadataFieldVOArray && curProfile.metadataFieldVOArray.length>0) {
						var curCustomData:SingleCustomData = new SingleCustomData();	
						curCustomData.formBuilder = formBuildersAC.getItemAt(i) as FormBuilder;
						curCustomData.addEventListener(SingleCustomData.SCROLL_TO_TOP, scrollToTop, false, 0, true);

						_profileNames.addItem(curProfile.profile.name);
						profilesVBox.addChild(curCustomData);			
					}
				}
				
				profilesVBox.verticalScrollPosition = 0;
			}
			
			private function scrollToTop(event:Event):void {
				profilesVBox.verticalScrollPosition  = 0;
			}
			

			protected function jumpToProfile():void
			{
				var scrollPos:int = 0;
				var childIndex:int = profilesCB.selectedIndex;
				if (childIndex!=-1) {
					//sums up all heights before the desired profile
					for (var i:int = 0; i< childIndex; i++) {
						scrollPos += profilesVBox.getChildAt(i).height + customDataVerticalGap;
					}
					profilesVBox.verticalScrollPosition = scrollPos;
				}
				profilesCB.selectedIndex = -1;
			}

		]]>
	</mx:Script>
	<mx:HBox id="navigationHbox" verticalAlign="middle">	
		<mx:ComboBox id="profilesCB" dataProvider="{_profileNames}" change="jumpToProfile()" prompt="{resourceManager.getString('drilldown','jumpToPrompt')}" 
					 selectedIndex="-1" fontWeight="bold" visible="{_profileNames.length > 1}" includeInLayout="{_profileNames.length > 1}"/>
	</mx:HBox>
	<mx:VBox id="profilesVBox" verticalGap="{customDataVerticalGap}" height="{this.height - navigationHbox.height - customDataVerticalGap}"
			 styleName="profilesVBox" width="100%"/>
</mx:VBox>
