<?xml version="1.0" encoding="utf-8"?>
<mx:DataGrid xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" rowHeight="40"
			 headerRelease="headerReleaseHandler(event)" itemClick="dispatchSelection(event)"
			 change="onSelectionsChange(event)" creationComplete="creationCompleteHandler(event)">
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.events.KmcHelpEvent;
			import com.kaltura.kmc.modules.account.events.ConversionSettingsEvent;
			import com.kaltura.kmc.modules.account.vo.ConversionProfileVO;
			
			import mx.core.Application;
			import mx.events.DataGridEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;


			/**
			 * value for the type property of the <code>editProfile</code> event
			 * */
			public static const EDIT_PROFILE:String = "editProfile";
			
			/**
			 * value for the type property of the <code>setAsDefaultProfile</code> event
			 * */
			public static const SET_AS_DEFAULT_PROFILE:String = "setAsDefaultProfile";


			/**
			 * prevent built in sorting 
			 * */
			protected function headerReleaseHandler(event:DataGridEvent):void {
				event.preventDefault();
			}


			protected function dispatchSelection(event:Event):void {
				for each (var cpVo1:ConversionProfileVO in this.dataProvider) {
					cpVo1.selected = false;
				}
				for each (var cpVo:ConversionProfileVO in this.selectedItems) {
					cpVo.selected = true && !cpVo.profile.isDefault;
					cpVo.dispatchEvent(new Event(ConversionSettingsEvent.MARK_CONVERSION_PROFILES));
				}
			}


			protected function onSelectionsChange(event:Event):void {
				var cpVo:ConversionProfileVO = this.selectedItem as ConversionProfileVO;
				if (cpVo != null) {
					if (cpVo.profile.isDefault) {
						this.selectedItem = null;
					}
				}
			}

			/**
			 * open drilldown window
			 * */
			protected function openProfileDrilldown(e:Event):void {
				var editCPWin:ConversionProfilePopUpWindow = new ConversionProfilePopUpWindow();
				editCPWin.currentState = ConversionProfilePopUpWindow.EDIT_STATE;
				editCPWin.conversionProfile = (e.target.data as ConversionProfileVO).clone();
				editCPWin.addEventListener(KmcHelpEvent.HELP, dispatchHelp, false, 0, true);
				PopUpManager.addPopUp(editCPWin, Application.application as DisplayObject, true);
				PopUpManager.centerPopUp(editCPWin);
			}
			
			
			private function dispatchHelp(e:KmcHelpEvent):void {
				dispatchEvent(e.clone());
			}

			/**
			 * listen to ItemRenderer's events
			 * */
			protected function creationCompleteHandler(event:FlexEvent):void {
				addEventListener(AdvancedConversionSettingsTable.EDIT_PROFILE, openProfileDrilldown);
				addEventListener(AdvancedConversionSettingsTable.SET_AS_DEFAULT_PROFILE, setDefaultProfile);
			}
			
			
			/**
			 * set the target profile as partner default
			 * */
			protected function setDefaultProfile(e:Event):void {
				var setDefault:ConversionSettingsEvent = new ConversionSettingsEvent(ConversionSettingsEvent.SET_AS_DEFAULT_CONVERSION_PROFILE);
				setDefault.data = (e.target.data as ConversionProfileVO).profile.id;
				setDefault.dispatch();
			}
		]]>
	</mx:Script>


	<mx:columns>
		<mx:DataGridColumn width="45" headerText="{resourceManager.getString('account','id')}" dataField="id"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.ConversionProfileIdRenderer"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('account','transNameCol')}" dataField="name"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.ConversionProfileNameRenderer"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('account','transDescCol')}" dataField="description"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.ConversionProfileDescriptionRenderer"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('account','transFlavSelcetedCol')}"
						   dataField="flavors" width="350"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.ConversionProfileFlavorsRenderer"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('account','transActionsCol')}" width="135"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.ConversionProfileActionRenderer"/>
	</mx:columns>
</mx:DataGrid>
