<?xml version="1.0" encoding="utf-8"?>
<mx:DataGrid xmlns:mx="http://www.adobe.com/2006/mxml" height="100%" verticalScrollPolicy="auto"
			 allowMultipleSelection="true" rowHeight="36" dropEnabled="true" dragMoveEnabled="false"
			 itemClick="setSelection(event);" creationComplete="creationCompleteHandler(event)"
			 xmlns:view="com.kaltura.kmc.modules.content.view.*">
	<mx:Metadata>
		[Event(name="drillDown", type="flash.events.Event")] 
		[Event(name="deleteUser", type="flash.events.Event")] 
		[Event(name="toggleBlock", type="flash.events.Event")] 
		[Event(name="selectUser", type="flash.events.Event")] 
	</mx:Metadata>

	<mx:Script>
		<![CDATA[
			import com.kaltura.types.KalturaUserStatus;
			import com.kaltura.vo.KalturaUser;
			import com.kaltura.vo.KalturaUserRole;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.ComboBox;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.formatters.DateFormatter;
			import mx.resources.ResourceManager;

			// ==================================================================
			// constants
			// ==================================================================

			/**
			 * defines the value of the type property for the <code>drillDown</code> event.
			 * */
			public static const DRILL_DOWN:String = "drillDown";

			/**
			 * defines the value of the type property for the <code>delete</code> event.
			 * */
			public static const DELETE_USER:String = "deleteUser";

			/**
			 * defines the value of the type property for the <code>toggleBlock</code> event.
			 * */
			public static const TOGGLE_BLOCK:String = "toggleBlock";

			/**
			 * defines the value of the type property for the <code>selectUser</code> event.
			 * */
			public static const SELECT_USER:String = "selectUser";
			
			

			// ==================================================================
			// members
			// ==================================================================
			
			[Bindable]
			/**
			 * dataprovider for the combobox 
			 */
			public var comboDataProvider:ArrayCollection;
			
			[Bindable]
			/**
			 * dp for current user and account administrator, only "edit" action.
			 * */
			private var crippledComboDataProvider:ArrayCollection;
			
			[Bindable][ArrayElementType("String")]
			/**
			 * ids of users who should get the cripled actions DP
			 * */
			public var crippledUsersIds:Array;
			
			
			
			[ArrayElementType("KalturaUserRole")]
			/**
			 * @copy #roles
			 * */
			private var _roles:ArrayCollection;
			
			
			// ==================================================================
			// methods
			// ==================================================================


			/**
			 * selection or deselection was made
			 */
			public function setSelection(event:ListEvent):void {
				if (event.itemRenderer.data) {
					dispatchEvent(new Event(UsersTable.SELECT_USER));
				}
			}


			/**
			 * open the entry drilldown popup
			 * */
			private function editEntry():void {
				dispatchEvent(new Event(UsersTable.DRILL_DOWN));
			}


			/**
			 * show delete confirmation box
			 * */
			private function confirmDelete():void {
				Alert.show(ResourceManager.getInstance().getString('admin', 'confirm_delete', [selectedUser.fullName]), ResourceManager.getInstance().getString('admin', 'confirm_delete_title'), Alert.OK | Alert.CANCEL, null, deleteEntry);

			}


			/**
			 * delete the selected entry
			 * */
			private function deleteEntry(e:CloseEvent):void {
				if (e.detail == Alert.OK) {
					// request to delete the entry
					dispatchEvent(new Event(UsersTable.DELETE_USER));
				}
			}


			/**
			 * show block toggle confirmation box
			 * */
			private function confirmBlock():void {
				var key:String = "confirm_unblock";
				if ((selectedItem as KalturaUser).status == KalturaUserStatus.ACTIVE) {
					key = "confirm_block";
				}
				Alert.show(ResourceManager.getInstance().getString('admin', key, [selectedUser.fullName]), ResourceManager.getInstance().getString('admin', key + '_title'), Alert.OK | Alert.CANCEL, null, toggleBlockEntry);
			}


			/**
			 * toggle block / unblock on the selected entry
			 * */
			private function toggleBlockEntry(e:CloseEvent):void {
				if (e.detail == Alert.OK) {
					// request to toggle user block status
					dispatchEvent(new Event(UsersTable.TOGGLE_BLOCK));
				}
			}
			
			
			protected function creationCompleteHandler(event:FlexEvent):void {
				comboDataProvider = new ArrayCollection();
				//build the dataprovider items list :
				comboDataProvider.addItem({label:resourceManager.getString('admin','edit'),id:'edit'});
				comboDataProvider.addItem({label:resourceManager.getString('admin','un_block'),id:'unblock'});
				comboDataProvider.addItem({label:resourceManager.getString('admin','delete'),id:'delete'});
				
				crippledComboDataProvider = new ArrayCollection();
				crippledComboDataProvider.addItem({label:resourceManager.getString('admin','edit'),id:'edit'});
				
			}


			// ==================================================================
			// item-renderer functions
			// ==================================================================

			public function getActionsDP(data:Object):ArrayCollection {
				var usr:KalturaUser = data as KalturaUser;
				for each (var uid:String in crippledUsersIds) {
					if (uid == usr.id) {
						return crippledComboDataProvider;
					}
				} 
				return comboDataProvider;
			}
			
			
			/**
			 * show formatted date
			 * */
			private function formatDate(date:Number):String {
				var df:DateFormatter = new DateFormatter();
				df.formatString = resourceManager.getString('admin', 'list_date_format');
				var dt:Date = new Date();
				dt.setTime(date * 1000);
				return df.format(dt);
			};


			/**
			 * translate user status to string
			 * */
			private function getUserStatus(item:Object, column:DataGridColumn):String {
				var res:String = '';
				switch (item.status) {
					case KalturaUserStatus.ACTIVE:
						res = ResourceManager.getInstance().getString('admin', 'active');
						break;
					case KalturaUserStatus.BLOCKED:
						res = ResourceManager.getInstance().getString('admin', 'blocked');
						break;
					case KalturaUserStatus.DELETED:
						res = ResourceManager.getInstance().getString('admin', 'deleted');
						break;
				}
				return res;
			}


			/**
			 * translate last login data to something readable
			 * */
			private function getLastLogin(item:Object, column:DataGridColumn):String {
				return formatDate(item.lastLoginTime);
			}


			/**
			 * translate role id to role name
			 * */
			private function getUserRole(item:Object, column:DataGridColumn):String {
				if (!_roles) return '';
				// need to scan all partner's roles list and get the correct one.
				for (var i:int = 0; i<_roles.length; i++) {
					if ((_roles.getItemAt(i) as KalturaUserRole).id == item.roleIds) {
						return (_roles.getItemAt(i) as KalturaUserRole).name;
					}
				}
				return item.roleIds;
			}
			

			/**
			 * Remove single item from the combobox dataprovider by its id
			 * @id - the name of the id to remove 
			 */
			private function removeItemById(id:String):void
			{
				//find the index of the item
				for (var i:uint=0;i<comboDataProvider.length;i++)
				{
					if(comboDataProvider.getItemAt(i).id == id)
						break;
				}
				//protect from an ID it did not find - check if this is the last item 
				// and if it has not the same id  
				if(i==comboDataProvider.length && comboDataProvider.getItemAt(i-1) != id )
					return;
				//remove the item
				comboDataProvider.removeItemAt(i); 
			}

			/**
			 * perform selected action on selected entry
			 * (used in item renderer)
			 * */
			public function onActionSelected(event:ListEvent):void {
				switch ((event.target as ComboBox).selectedItem.id) {
					case 'edit':
						editEntry();
						break;
					case 'unblock':
						confirmBlock();
						break;
					case 'delete':
						confirmDelete();
						break;
				}
				(event.target as ComboBox).selectedIndex = -1;
			}
			
			// ==================================================================
			// gettesr / setters
			// ==================================================================

			public function get selectedUser():KalturaUser {
				return selectedItem as KalturaUser;
			}
			
			/**
			 * Hide the Edit action from the data provider. 
			 * @param value
			 */
			public function set enableEdit(value:Boolean):void
			{
				removeItemById('edit');
			}
			/**
			 * @private 
			 * PermissionsManager has to read the value to know the attribute type
			 */
			public function get enableEdit():Boolean 
			{
				return true;
			}
			/**
			 * Hide the Block/Unblock action from the data provider. 
			 * @param value
			 */
			public function set enableBlock(value:Boolean):void
			{
				removeItemById('unblock');
			}
			/**
			 * @private 
			 * PermissionsManager has to read the value to know the attribute type
			 */
			public function get enableBlock():Boolean
			{
				return true;
			}
			
			/**
			 * Hide the Delete action from the data provider. 
			 * @param value
			 */
			public function set enableDelete(value:Boolean):void
			{
				removeItemById('delete');
			}
			/**
			 * @private 
			 * PermissionsManager has to read the value to know the attribute type
			 */
			public function get enableDelete():Boolean
			{
				return true;
			}

			/**
			 * list of all the roles associated with the current partner
			 * (KalturaUserRole objects)
			 * */
			public function set roles(value:ArrayCollection):void {
				_roles = value;
				dataProvider = dataProvider;
			}
			
			

		]]>
	</mx:Script>

	<mx:columns>
		<mx:DataGridColumn dataField="status" labelFunction="{getUserStatus}"
						   headerText="{resourceManager.getString('admin', 'user_status')}"
						   headerStyleName="adminTableColumn" />
		<mx:DataGridColumn dataField="fullName"
						   headerText="{resourceManager.getString('admin', 'user_name')}"
						   headerStyleName="adminTableColumn"/>
		<mx:DataGridColumn dataField="id"
						   headerText="{resourceManager.getString('admin', 'id')}"
						   headerStyleName="adminTableColumn"/>
		<mx:DataGridColumn dataField="email"
						   headerText="{resourceManager.getString('admin', 'user_email')}"
						   headerStyleName="adminTableColumn"/>
		<mx:DataGridColumn dataField="roleIds" labelFunction="{getUserRole}"
						   headerText="{resourceManager.getString('admin', 'role')}"
						   headerStyleName="adminTableColumn"/>
		<mx:DataGridColumn dataField="lastLoginTime" labelFunction="{getLastLogin}"
						   headerText="{resourceManager.getString('admin', 'user_last_login')}"
						   headerStyleName="adminTableColumn"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('admin', 'user_actions')}"
						   headerStyleName="adminTableCenteredColumn" >
			<mx:itemRenderer>
				<mx:Component>
					<mx:VBox width="100%" height="100%" verticalAlign="middle" horizontalAlign="center"
							 horizontalScrollPolicy="off" verticalScrollPolicy="off">
						<mx:ComboBox width="130" selectedIndex="-1"
									 dataProvider="{outerDocument.getActionsDP(data)}"
									 prompt="{resourceManager.getString('admin','select_action')}"
									 change="{outerDocument.onActionSelected(event)}" />
					</mx:VBox>
				</mx:Component>
			</mx:itemRenderer>
		</mx:DataGridColumn>
	</mx:columns>
</mx:DataGrid>
